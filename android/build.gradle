buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

plugins {
    id "de.undercouch.download"
}

group 'io.gitjournal.git_bindings'
version '1.0'

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        sourceSets {
            main {
                jniLibs.srcDirs = ['libs']
                java.srcDirs = ['src']
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
    // For reading a file to string
    implementation 'commons-io:commons-io:2.5'
}

task extractLibGit2 {
    doLast {
        download {
            src 'https://github.com/GitJournal/ndk-libraries/releases/download/v0.2/libs.tar'
            dest 'libs.tar.gz'
            overwrite false
        }
        copy {
            from tarTree(resources.gzip("libs.tar.gz"))
            into 'libs'
        }
        copy {
            from 'libs/libs/openssl-lib/'
            into 'libs/'
        }
        copy {
            from 'libs/libs/libssh2/'
            into 'libs/'
        }
        copy {
            from 'libs/libs/libgit2/'
            into 'libs/'
        }
    }
}

preBuild.dependsOn extractLibGit2
